#include <cuda_runtime.h>

#define TILE 16

__global__ void gemm_tiled(const float* A, const float* B, float* C,
                           int M, int K, int N) {
    __shared__ float As[TILE][TILE];
    __shared__ float Bs[TILE][TILE];

    int row = blockIdx.y * TILE + threadIdx.y;
    int col = blockIdx.x * TILE + threadIdx.x;

    float acc = 0.0f;

    for (int t = 0; t < (K + TILE - 1) / TILE; t++) {
        int tiledRow = row;
        int tiledCol = t * TILE + threadIdx.x;
        if (tiledRow < M && tiledCol < K)
            As[threadIdx.y][threadIdx.x] = A[tiledRow * K + tiledCol];
        else
            As[threadIdx.y][threadIdx.x] = 0.0f;

        tiledRow = t * TILE + threadIdx.y;
        tiledCol = col;
        if (tiledRow < K && tiledCol < N)
            Bs[threadIdx.y][threadIdx.x] = B[tiledRow * N + tiledCol];
        else
            Bs[threadIdx.y][threadIdx.x] = 0.0f;

        __syncthreads();

        for (int k = 0; k < TILE; k++)
            acc += As[threadIdx.y][k] * Bs[k][threadIdx.x];

        __syncthreads();
    }

    if (row < M && col < N)
        C[row * N + col] = acc;
}

